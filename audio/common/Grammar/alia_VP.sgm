// VERB PHRASES
// ================================================
 
// declarative sentence

=[fact]
  <np> <vp> ((<np>) <np>)
  you <ACT> ((<np>) <np>)
  <np> <vp> (<np>) to <cmd>
  <%obj-i0> <$add>
  <%obj> <$add>
  there is <%obj-i>


// action to take

=[cmd]
  <!do>
  <find>
  <!chk>


// --------------------------------------------------

// verb phrase

=[vp]
  <ACT-S>
  <ACT-D>


// negated verb

=[NEG-V]
  don't 
  do not


// set of verb modifiers

=[vmod3]
  <vmod> (<vmod> (<vmod>))


// generic verb phrase modifier

=[vmod]
  <AMT>
  <dir3>
  <manner>
  <$rel>


// possibly compound direction

=[dir3]
  <DIR> ((<DIR>) and <DIR>)


// manner of acting

=[manner]
  (<DEG>) <MOD> 


// verb adjunct (prepositional phrase) 

=[$rel]
  <place>


// ==================================================

// DO directive ("you" or missing subject, 0 to 2 objects)

=[!do]
  (you <PREF>) (<vmod3>) <ACT> (<np> (<np>)) (<vmod3>)
  (you <PREF>) (<vmod3>) <SAY> <QUOTE>


// request to echo something

=[SAY]
  say


// from 1 to 16 words

=[QUOTE]
 # (# (# (# (# (# (# (# (# (# (# (# (# (# (# (#)))))))))))))))


// --------------------------------------------------

// FIND directive

=[find]
 <find-v> <!find-ako>
 <find-v> <!find-hq>


// find verb phrase

=[find-v]
  find (out) 


// embedded question about type

=[!find-ako]
  what <np> is
  what kind of <AKO> <np> is


// embedded question about property

=[!find-hq]
  what <AKO> <np> is
  what <np-poss> <AKO> is


// --------------------------------------------------

// CHECK directive

=[!chk]
  <check-v> <fact>


// check verb phrase

=[check-v]
  check (if) 



